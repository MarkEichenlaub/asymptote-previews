//-----------------  ESSENTIAL OPTIONS ---------------

//heights and labels for columns
real[] energies = {.6, 0, .4};
string[] energy_types = {"Kinetic", "Potential", "Thermal"};

bool label_energies = false; //writes out the energy above each column

bool show_scale = false; // whether or not to show labels and ticks on $y$ axis
bool show_total = true; //add auto-calculated "total energy" bar at the end
bool show_y_label = true; //whether to write "Energy (J)" or similar on y-axis

string unit = ""; //symbol for energy units, delete the J for no units

//-----------------  AESTHETIC TWEAKS --------------------

//attempts to mimic "Energy Skate Park" by default
//https://phet.colorado.edu/sims/html/energy-skate-park/latest/energy-skate-park_en.html

//size of image in cm
real width = 6;
real height = 8;

//column colors
//reference: https://asymptote.sourceforge.io/doc/Pens.html
pen[] colors = {heavygreen, royalblue*.85, .7*orange+ .3*red, olive*1.3};

//style of labels for the columns
//reference: https://asymptote.sourceforge.io/doc/Pens.html
pen labels = Helvetica();

// increase to make bars wider
real bar_width = 0.55;

// ---------------  BELOW HERE YOU USUALLY DON'T NEED TO CHANGE THINGS ------------

import graph;

unit = "$\mathrm{" + unit + "}$";

size(width*72/2.54, height*72/2.54, IgnoreAspect);

energies.cyclic = true; //allows getting last energy with [-1] index
colors.cyclic = true; //allows recycling if too few colors given

if(energies.length != energy_types.length){
label("energies and energy types aren't the same length");
}

if(show_total){
energies.push(sum(energies));
energy_types.push("Total");
}

int num_energies = energies.length;

bar_width = 1/num_energies * bar_width;

real max_energy = max(energies);

Label tickFormat = "%";
if(show_scale){ tickFormat = "";}

ticks yticks = RightTicks(format = tickFormat);
if(! show_scale){
yticks = NoTicks;
}

string ylabel = "";
if(show_y_label){
if(unit == "$\mathrm{}$" || unit == "$\mathrm{ }$"){
ylabel = "Energy";
}
else{
ylabel = "Energy \,\,(" + unit+")";
}
}

// -----------------  DO THE ACTUAL DRAWING   --------------------

for(int i = 0; i< num_energies; ++i){
filldraw(shift(bar_width/2,0)*shift(i/num_energies,0)*box((0,0),(bar_width,energies[i])), colors[i] );
label(rotate(90)*energy_types[i],(bar_width/2 + i/num_energies + bar_width/2,0),S, labels);
if (label_energies){
label(string(energies[i])+"\;"+unit,(bar_width + i/num_energies,energies[i]),N);
}
}

xaxis(0, 1);
yaxis(0, max_energy, L = ylabel, ticks = yticks);