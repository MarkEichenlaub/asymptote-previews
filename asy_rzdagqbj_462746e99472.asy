//this is a template for creating blank axes
//change the variables below to suit your needs, then click "RENDER AS IMAGE" above. right click and download the image

//------ DATA -----------------

bool plotPoints = true;
bool plotCurve = false;

real[] xdata = {1,2,3,4, 5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40}; 
real x_transform(real x){ 
return x; //change to, e.g. "return x^2;" to plot x^2 on the horizontal axis. 
}

//real[] ydata_p0 = {4.9,4.9,4.89,4.89,4.89, 4.89,4.87,4.85,4.85,4.8,4.72,4.48,4.08,3.69,3.27,2.69,2.3,2,1.74,1.52,1.35,1.27};

real[] ydata_p0 = {
    5.0000, 5.0000, 5.0000, 5.0000, 5.0000,
    5.0000, 5.0000, 5.0000, 5.0000, 5.0000,
    5.0000, 5.0000, 5.0000, 5.0000, 5.0000,
    5.0000, 4.8824, 4.5556, 4.2632, 4.0000,
    3.7619, 3.5455, 3.3478, 3.1667, 3.0000,
    2.8462, 2.7037, 2.5714, 2.4483, 2.3333,
    2.2258, 2.1250, 2.0303, 1.9412, 1.8571,
    1.7778, 1.7027, 1.6316, 1.5641, 1.5000
};

real[] ydata_p0_1 = {
    4.8997, 4.8982, 4.8975, 4.8963, 4.8957,
    4.8931, 4.8898, 4.8880, 4.8848, 4.8797,
    4.8720, 4.8613, 4.8452, 4.8083, 4.7106,
    4.4347, 4.0148, 3.6806, 3.4330, 3.2314,
    3.0396, 2.8682, 2.7213, 2.5718, 2.4452,
    2.3150, 2.2011, 2.0971, 2.0018, 1.9116,
    1.8245, 1.7449, 1.6697, 1.5993, 1.5306,
    1.4680, 1.4062, 1.3488, 1.2964, 1.2446
};

real[] ydata_p0_2 = {
    4.8021, 4.7965, 4.7960, 4.7941, 4.7907,
    4.7892, 4.7833, 4.7790, 4.7702, 4.7598,
    4.7445, 4.7168, 4.6431, 4.4160, 3.9453,
    3.5126, 3.2260, 3.0047, 2.8287, 2.6568,
    2.5001, 2.3601, 2.2362, 2.1232, 2.0159,
    1.9147, 1.8181, 1.7383, 1.6600, 1.5843,
    1.5160, 1.4441, 1.3867, 1.3303, 1.2772,
    1.2212, 1.1738, 1.1283, 1.0830, 1.0402
};

real[] ydata_p0_3 = {
    4.6974, 4.6972, 4.6923, 4.6911, 4.6867,
    4.6832, 4.6760, 4.6678, 4.6574, 4.6390,
    4.6010, 4.4801, 4.0502, 3.4665, 3.1062,
    2.8574, 2.6703, 2.4961, 2.3417, 2.1992,
    2.0794, 1.9623, 1.8579, 1.7635, 1.6731,
    1.5957, 1.5143, 1.4449, 1.3779, 1.3173,
    1.2581, 1.2048, 1.1582, 1.1079, 1.0605,
    1.0196, 0.9800, 0.9402, 0.9033, 0.8683
};

real[] ydata_p0_4 = {
    4.6013, 4.5999, 4.5943, 4.5895, 4.5849,
    4.5795, 4.5704, 4.5597, 4.5412, 4.4996,
    4.2828, 3.6818, 3.0750, 2.7661, 2.5401,
    2.3565, 2.1971, 2.0608, 1.9377, 1.8148,
    1.7127, 1.6251, 1.5383, 1.4543, 1.3785,
    1.3158, 1.2536, 1.1951, 1.1397, 1.0908,
    1.0449, 1.0022, 0.9563, 0.9178, 0.8832,
    0.8472, 0.8115, 0.7801, 0.7483, 0.7205
};

real[] ydata_p0_5 = {
    4.5015, 4.4978, 4.4928, 4.4908, 4.4838,
    4.4785, 4.4696, 4.4492, 4.4083, 4.1335,
    3.3483, 2.7307, 2.4374, 2.2412, 2.0711,
    1.9308, 1.8030, 1.6738, 1.5751, 1.4838,
    1.4042, 1.3256, 1.2514, 1.1862, 1.1269,
    1.0708, 1.0225, 0.9731, 0.9341, 0.8910,
    0.8504, 0.8170, 0.7806, 0.7472, 0.7189,
    0.6893, 0.6616, 0.6360, 0.6109, 0.5867
};

real y_transform(real y){
return y;
}

//define what curve to draw
real curve(real x){
return x^2;  //change to the equation for your curve
}

//style of graph line
pen graphpen = deepred+linewidth(1.0 pt);

//style of dots
dotfactor = 10; //size
pen dotpen = blue; 
filltype dotfill = Fill; //can change to "NoFill" for hollow dots

//-------ESSENTIAL OPTIONS------------

string xAxisLabel = "cars"; //for no label, delete the stuff between the quotes
string yAxisLabel = "long term average speed";

real xmin = 0; 
real xmax = 40;
real ymin = 0;
real ymax = 5;

real xTickSpacing = 5; 
real xSubtickSpacing = 1; //set to xTickSpacing if you don't want little ticks
real yTickSpacing = 1;
real ySubtickSpacing = 0.25;

//-------------ENTIRE IMAGE------------

real xsize = 15; //in cm
real ysize = 10;

//next line only used if you also set "equalAxisRatio" to "true" in the "AXES" section
real overallSize = 30;

//----------- LABEL OPTIONS ---------------

real xlabel_loc = 1; //what fraction down the axis you want the label
real ylabel_loc = 1;

real labelSize = 12;

//------------ AXES ----------

bool equalAxisRatio = false; //when true, 1 unit is same size on both axes. overwrites ysize

bool fourSides = true; //if true, draw axes on the right and top as well as left and bottom

bool xAxisRightArrow = false; //puts arrows at the end of the axes
bool xAxisLeftArrow = false;
bool yAxisUpArrow = false;
bool yAxisDownArrow = false;

bool forcexAxisAtZero = false; //x axis at y = 0, even if y extends negative
bool forceyAxisAtZero = false;

//-----------  TICK MARKS ----------------
bool showTicks = true;

bool ticksGoThroughAxes = false;

real tickSize = 6;
real subtickSize = 3;

// -----------  NUMBERING ON AXES -----------

bool axisNumbers = true;
real numberSize = 12;

bool controlDecimals = false; //next two don't do anything unless this is "true"
bool trailingZeros = true;
int numDecimalPoints = 1;

//------------ GRID -------------------

//to adjust the grid spacing, change the tick spacing

bool bigGrid = true; //gridlines for the labeled tick marks
bool smallGrid = true; //gridlines for the smaller subticks

//------------ TITLE -----------

string title = "Nagel-Schreckenberg Model with 100 cells"; 
real titleSize = 15;

//--------BELOW HERE YOU USUALLY DON'T NEED TO CHANGE ANYTHING------------------------

//for documentation, see https://asymptote.sourceforge.io/doc/graph.html

if(equalAxisRatio){
unitsize(overallSize);
}
else{
size(xsize*72/2.54, ysize*72/2.54, IgnoreAspect);
}

import graph;

arrowbar xarrow = None;
if(xAxisRightArrow && xAxisLeftArrow){
xarrow = Arrows;
}
else{
	if(xAxisRightArrow){
		xarrow = EndArrow;
        }
    if(xAxisLeftArrow){
		xarrow = BeginArrow;
	}
}

arrowbar yarrow = None;
if(yAxisUpArrow && yAxisDownArrow){
yarrow = Arrows;
}
else{
	if(yAxisUpArrow){
		yarrow = EndArrow;
	}
    if(yAxisDownArrow){
		yarrow = BeginArrow;
    }
}

Label xLabel = Label(s = xAxisLabel, position = xlabel_loc, p = fontsize(labelSize), filltype = Fill(p = white) );
Label yLabel = Label(s = yAxisLabel, position = ylabel_loc, p = fontsize(labelSize),filltype = Fill(p = white) );

real killZero = 9805675997679076690980969.000123;

axis xAxisType = Bottom;
axis yAxisType = Left;
if(fourSides){
xAxisType = BottomTop;
yAxisType = LeftRight;
}
if(forcexAxisAtZero){
xAxisType = YZero;
}
if(forceyAxisAtZero){
yAxisType = XZero;
killZero = 0;
}

Label tickFormat = Label(" ");

string decimalString = "";
if(controlDecimals){
	if(trailingZeros){
		decimalString = "%#." + string(numDecimalPoints) + "f";
        }
    else{
    	decimalString = "%." + string(numDecimalPoints) + "f";
        }
}

if(axisNumbers){
tickFormat = Label(decimalString, filltype = Fill(p = white), p = fontsize(numberSize) );
}

if(!showTicks){
subtickSize = -1;
tickSize = -1;
}

ticks xticks = LeftTicks(format = tickFormat, Step = xTickSpacing, step = xSubtickSpacing, size = subtickSize, Size = tickSize);
ticks yticks = RightTicks(format = tickFormat, Step = yTickSpacing, step = ySubtickSpacing, size = subtickSize, Size = tickSize );

if(ticksGoThroughAxes){
xticks = Ticks(format = tickFormat, xTickSpacing, xSubtickSpacing, size = subtickSize, Size = tickSize);
yticks = Ticks(format = tickFormat, yTickSpacing, ySubtickSpacing, size = subtickSize, Size = tickSize);
}

dot((xmin,ymin), black+0);// so the graph package knows where to place the top and bottom x axes
dot((xmax,ymax),black+0);

//----------------DO THE DRAWING----------------

xaxis(xmin = xmin, xmax = xmax, L = xLabel, axis = xAxisType, arrow = xarrow, ticks = xticks);
yaxis(ymin = ymin, ymax = ymax, L = yLabel, axis = yAxisType, arrow = yarrow, ticks = yticks);

if(bigGrid){
xaxis(LeftRight, invisible, Ticks(Step = xTickSpacing, extend = true, gray) );
yaxis(BottomTop, invisible, Ticks(Step = yTickSpacing, extend = true, gray) );
}

if(smallGrid){
xaxis(LeftRight, invisible, Ticks(Step = xSubtickSpacing, extend = true, lightgray) );
yaxis(BottomTop, invisible, Ticks(Step = ySubtickSpacing, extend = true, lightgray) );
}

label(title, (xmax/2 + xmin/2, ymax),N, fontsize(titleSize));

real[] x_to_plot = map(x_transform, xdata);

draw(graph(x_to_plot, ydata_p0, operator ..), p = purple+linewidth(2pt));
draw(graph(x_to_plot, ydata_p0_1, operator ..), p = blue+linewidth(2pt));
draw(graph(x_to_plot, ydata_p0_2, operator ..), p = deepgreen+linewidth(2pt));
draw(graph(x_to_plot, ydata_p0_3, operator ..), p = olive+linewidth(2pt));
draw(graph(x_to_plot, ydata_p0_4, operator ..), p = orange+linewidth(2pt));
draw(graph(x_to_plot, ydata_p0_5, operator ..), p = red+linewidth(2pt));

if(plotCurve){
draw(graph(curve, xmin, xmax), p = graphpen);}

//label("$p = 0.1, 100 $ cells",(20,5),N);

// Add legend
pen[] pens = {
    purple+linewidth(2pt),
    blue+linewidth(2pt),
    deepgreen+linewidth(2pt),
    olive+linewidth(2pt),
    orange+linewidth(2pt),
    red+linewidth(2pt)
};

string[] labels = {
    "p = 0.0",
    "p = 0.1",
    "p = 0.2",
    "p = 0.3",
    "p = 0.4",
    "p = 0.5"
};

// Positioning for the legend
real legend_x = xmin + .75*(xmax - xmin); // x position of the legend
real legend_y = ymin + 0.95*(ymax - ymin); // y position of the legend
real legend_dy = 0.4; // Vertical spacing between legend entries

// Loop to draw legend entries
for(int i = 0; i < pens.length; ++i){
    // Draw a line segment for the legend entry
    draw( ( (legend_x, legend_y - i*legend_dy) -- (legend_x + 2, legend_y - i*legend_dy) ), pens[i]);
    // Add label next to the line segment
    label(labels[i], (legend_x+2 , legend_y - i*legend_dy), align = E, p = fontsize(10));
}