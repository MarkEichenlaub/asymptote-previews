// Fill in the energies you want in line 8. For example, for 3 J kinetic energy, 4 J potential energy, and 0 J thermal energy, change "{1, 0, 0}" to "{3, 4, 0}" in line 8
// Then hit "RENDER AS IMAGE" and right click to download your chart
// There are various other features if you want to customize things

//-----------------  DATA TO PLOT ---------------

real[] energies = {0, 1, 0}; //you can make this as many types of energy as you want, but update line 9 as well
string[] energy_types = {"Kinetic", "Potential", "Thermal"};
string unit = ""; //symbol for energy units, delete the J (but leave the quotes) for no units

string title = "Initial Condition";

//----------------- TURN THINGS ON AND OFF --------------

bool show_total = true; //add auto-calculated "total energy" bar at the end

bool show_values_for_energies = true; //writes out the energy above each column

bool show_ticks = false; // whether or not to show ticks on $y$ axis
bool show_scale = false; // whether or not to show labels on $y$ axis; draws ticks even if "show_ticks" is false
bool show_y_label = true; //whether to write "Energy (J)" or similar on y-axis

bool show_title = true;

//-----------------  AESTHETIC TWEAKS --------------------

//attempts to mimic "Energy Skate Park" by default
//https://phet.colorado.edu/sims/html/energy-skate-park/latest/energy-skate-park_en.html

//size of image in cm
real width = 6;
real height = 8;

//column colors
//reference: https://asymptote.sourceforge.io/doc/Pens.html
pen[] colors = {heavygreen, royalblue*.85, .7*orange+ .3*red, olive*1.3};

//style of labels for the columns
//reference: https://asymptote.sourceforge.io/doc/Pens.html
pen labels = Helvetica();

// increase to make bars wider
real bar_width = 0.55;

// ---------------  BELOW HERE YOU USUALLY DON'T NEED TO CHANGE THINGS ------------

import graph;

unit = "$\mathrm{" + unit + "}$";

size(width*72/2.54, height*72/2.54, IgnoreAspect);

energies.cyclic = true; //allows getting last energy with [-1] index
colors.cyclic = true; //allows recycling if too few colors given

if(energies.length != energy_types.length){
label("energies and energy types aren't the same length");
}

if(show_total){
energies.push(sum(energies));
energy_types.push("Total");
}

int num_energies = energies.length;

bar_width = 1/num_energies * bar_width;

real max_energy = max(energies);

ticks yticks = RightTicks(format = "%");
if(! show_ticks){ yticks = NoTicks;}
if(show_scale){ yticks = RightTicks();}

string ylabel = "";
if(show_y_label){
if(unit == "$\mathrm{}$" || unit == "$\mathrm{ }$"){
ylabel = "Energy ($mgh$)";
}
else{
ylabel = "Energy \,\,(" + unit+")";
}
}

// -----------------  DO THE ACTUAL DRAWING   --------------------

for(int i = 0; i< num_energies; ++i){
// bars
filldraw(shift(bar_width/2,0)*shift(i/num_energies,0)*box((0,0),(bar_width,energies[i])), colors[i] );

//labels
label(rotate(90)*energy_types[i],(bar_width/2 + i/num_energies + bar_width/2,0),S, labels);

if (show_values_for_energies){
label(string(energies[i])+"\;"+unit,(bar_width + i/num_energies,max(0,energies[i])),N);
}
}

xaxis(0, 1);
yaxis(0, max_energy, L = ylabel, ticks = yticks);

if(show_title){label(title, (.5,max_energy*1.15),N);}